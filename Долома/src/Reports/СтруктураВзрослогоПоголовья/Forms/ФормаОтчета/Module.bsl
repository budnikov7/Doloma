
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	ЗначениеВарианта = "";
	Параметры.Свойство("КлючВарианта",ЗначениеВарианта);
	Если ЗначениеВарианта = Неопределено Тогда
		УстановитьТекущийВариант("Основной");
	КонецЕсли;  
	
КонецПроцедуры
  
&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если НЕ РольДоступна("ПолныеПрава") И НЕ РольДоступна("ДоступКоВсемФермам") Тогда
		КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
		НастройкиОтчета = КомпоновщикНастроекФормы.ФиксированныеНастройки; 
		ПолеСКДФерма = Новый ПолеКомпоновкиДанных("Ферма");
		ЭлементФерма = ВернутьЭлементОтбора(ПолеСКДФерма,НастройкиОтчета);
		Если ЭлементФерма = Неопределено Тогда
			ЭлементФерма = НастройкиОтчета.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
			ЭлементФерма.ЛевоеЗначение = ПолеСКДФерма;
		КонецЕсли; 
		ЭлементФерма.ПравоеЗначение = ПараметрыСеанса.ТекущийПользователь.ОсновнаяФерма;  
		ЭлементФерма.ВидСравнения  = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементФерма.Использование = Истина;
		
		ЭлементФерма = ВернутьЭлементОтбора(ПолеСКДФерма, КомпоновщикНастроекФормы.Настройки);
		Если ЭлементФерма <> Неопределено Тогда
		    Настройки = КомпоновщикНастроекФормы.Настройки;
			Настройки.Отбор.Элементы.Удалить(ЭлементФерма); 
			КомпоновщикНастроекФормы.ЗагрузитьНастройки(Настройки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры  

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	ПриОткрытииНаСервере();
КонецПроцедуры

Функция ВернутьЭлементОтбора(ПолеСКД,НастройкиОтчета)

	ЭлементыОтбора = НастройкиОтчета.Отбор.Элементы; 
	НайденныйЭлемент = Неопределено;
	Для каждого Элемент Из ЭлементыОтбора Цикл
		Если Элемент.ЛевоеЗначение = ПолеСКД Тогда
			НайденныйЭлемент = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденныйЭлемент;

КонецФункции 

&НаСервере
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки) 
	
	ПериодРасшифровки = Неопределено;
	Если Параметры.Свойство("ПериодРасшифровки",ПериодРасшифровки) Тогда
		УстановитьПользовательскийПараметрСКД(Настройки, "ПериодОтчета", ПериодРасшифровки);
	КонецЕсли;  
	ИмяПоляОтбора = Неопределено;
	Если Параметры.Свойство("ИмяОтбора",ИмяПоляОтбора) Тогда
		УстановитьОтборСКД(ИмяПоляОтбора);
	КонецЕсли; 
	
КонецПроцедуры

//РАСШИФРОВКА

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
    Если ТипЗнч(Элемент.ТекущаяОбласть) = Тип("РисунокТабличногоДокумента") Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь; 
	СтруктураРасшифровки = Новый Структура;
	ПолучитьСтруктуруРасшифровки(СтруктураРасшифровки, Расшифровка); 
	
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(Отчет));
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВыборДействияЗавершение", ЭтаФорма, СтруктураРасшифровки);
	ДоступныеДействия = Новый Массив;
	
	ДополнительныеПунктыМеню = Новый СписокЗначений;  
	Для каждого ПолеРасшифровки Из СтруктураРасшифровки Цикл  
		Если СтрНайти(ПолеРасшифровки.Ключ,"Животное") Тогда  
			ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
		КонецЕсли;	
	КонецЦикла; 
	
	Если ДоступныеДействия.Количество() = 0 Тогда
		ДополнительныеПунктыМеню.Добавить("Открыть отчет расшифровку");
	КонецЕсли;
	
	ОбработкаРасшифровки.ПоказатьВыборДействия(ОписаниеОповещения, Расшифровка, ДоступныеДействия, ДополнительныеПунктыМеню);
	
КонецПроцедуры   

&НаКлиенте
Процедура ПоказатьВыборДействияЗавершение(ВыполненноеДействие, ПараметрВыполненногоДействия, ДополнительныеПараметры) Экспорт
	
	Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		ПоказатьЗначение(Неопределено, ПараметрВыполненногоДействия);
	ИначеЕсли  ВыполненноеДействие = "Открыть отчет расшифровку" Тогда 
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
			СтруктураОтбора = Новый Структура; 
			ИмяРесурса = Неопределено;
			Для каждого ЭлементСтруктуры Из ДополнительныеПараметры Цикл  
				Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Число") Тогда
					ИмяРесурса = ЭлементСтруктуры.Ключ; 
				ИначеЕсли ЗначениеЗаполнено(ЭлементСтруктуры.Значение) Тогда
					СтруктураОтбора.Вставить(ЭлементСтруктуры.Ключ,ЭлементСтруктуры.Значение);
				КонецЕсли;
			КонецЦикла;
			ОткрытьОтчётСОтбором(СтруктураОтбора,"Расшифровка",ИмяРесурса);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ПолучитьСтруктуруРасшифровки(СтруктураПолей, Расшифровка)
	
	ДанныеРасшифровкиКомпоновки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ПолучитьЗначенияПолей(СтруктураПолей, ДанныеРасшифровкиКомпоновки.Элементы, Расшифровка);
	
	ЭлементРасшифровки = ДанныеРасшифровкиКомпоновки.Элементы.Получить(Расшифровка);
	ПолучитьЗначениеПоля(СтруктураПолей, ЭлементРасшифровки);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗначенияПолей(СтруктураПолей, ЭлементыРасшифровки, Расшифровка)
	
	Для Каждого Родитель Из ЭлементыРасшифровки.Получить(Расшифровка).ПолучитьРодителей() Цикл
		
		ПолучитьЗначенияПолей(СтруктураПолей, ЭлементыРасшифровки, Родитель.Идентификатор);
		ПолучитьЗначениеПоля(СтруктураПолей, Родитель);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗначениеПоля(СтруктураПолей, ЭлементРасшифровки)
	
	Если НЕ ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
		
		Для Каждого Поле Из ЭлементРасшифровки.ПолучитьПоля() Цикл
			Если ТипЗнч(Поле) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
				СтруктураПолей.Вставить(Поле.Поле, Поле.Значение);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры   


&НаКлиенте
Процедура ОткрытьОтчётСОтбором(ДополнительныеПараметры,НаименованиеВариантаОтчета,ИмяРесурса)
	
	НастройкиЗдесь = Отчет.КомпоновщикНастроек.Настройки;  
	НастройкиПользовательскиеЗдесь = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	ИДНастройкиПериодЗдесь = НастройкиЗдесь.ПараметрыДанных.Элементы.Найти("ДатаОкончания").ИдентификаторПользовательскойНастройки;
	НайденныйПараметрПериод = НастройкиПользовательскиеЗдесь.Элементы.Найти(ИДНастройкиПериодЗдесь);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", НаименованиеВариантаОтчета);
	ПараметрыФормы.Вставить("ИмяОтбора", ИмяРесурса);
	Если НайденныйПараметрПериод <> Неопределено Тогда
		ПериодОтчета = НайденныйПараметрПериод.Значение;
		ПараметрыФормы.Вставить("ПериодРасшифровки", ПериодОтчета);
	КонецЕсли;
	ПараметрыФормы.Вставить("Отбор", ДополнительныеПараметры);
	ОткрытьФорму(ЭтотОбъект.ИмяФормы, ПараметрыФормы, , Новый УникальныйИдентификатор);
	
КонецПроцедуры //  КомандаОткрытьОтчётСОтбором     

&НаСервере
Процедура УстановитьПользовательскийПараметрСКД(Настройки, ИмяПараметра, Значение)
	
	НастройкиЗдесь = Отчет.КомпоновщикНастроек.Настройки;  
	ПараметрДанных = НастройкиЗдесь.ПараметрыДанных.Элементы.Найти("ДатаОкончания");
	
	Если ЗначениеЗаполнено(ПараметрДанных.ИдентификаторПользовательскойНастройки) Тогда
		ПользовательскийПараметр = Настройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		ПользовательскийПараметр.Использование = Истина;
		ПользовательскийПараметр.Значение = Значение;
	КонецЕсли;
	
КонецПроцедуры  //  УстановитьПользовательскийПараметрСКД  

&НаСервере
Процедура УстановитьОтборСКД(ИмяПараметра)
	
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	ПолеСКД = Новый ПолеКомпоновкиДанных(ИмяПараметра); 
	ЭлементОтбора = Неопределено;
	Для Каждого ЭлементНастройки Из Настройки.Отбор.Элементы Цикл
		Если ЭлементНастройки.ЛевоеЗначение = ПолеСКД Тогда 
			ЭлементОтбора = ЭлементНастройки;
	        Прервать;
	    КонецЕсли;
	КонецЦикла; 
	Если ЭлементОтбора = Неопределено Тогда
		ЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение  = ПолеСКД;
	КонецЕсли;
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбора.Использование = Истина;
	
	
КонецПроцедуры 
