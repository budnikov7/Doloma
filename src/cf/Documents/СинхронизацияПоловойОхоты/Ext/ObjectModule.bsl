
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ЖивотныеНаСхемеСинхронизации.Записывать = Истина; 
	Движения.СостояниеЖивотных.Записывать = Истина;

	Выборка = ВыборкаЖивотных();
	
	Пока Выборка.Следующий() Цикл
		НовоеДвижение 						= Движения.ЖивотныеНаСхемеСинхронизации.Добавить();
		НовоеДвижение.Период 				= Дата;
		НовоеДвижение.Животное 				= Выборка.Животное;
		НовоеДвижение.СхемаСинхронизации 	= СхемаСинхронизации;
		НовоеДвижение.Событие 				= Перечисления.СобытияСхемСинхронизации.Начало;
		
		Если Выборка.СостояниеЖивотного = Перечисления.СтатусЖивотного.Осеменена Тогда
			НовоеДвижение 					= Движения.СостояниеЖивотных.Добавить();
			НовоеДвижение.Животное 			= Выборка.Животное;
			НовоеДвижение.Период 			= Дата;
			НовоеДвижение.СостояниеЖивотного= Перечисления.СтатусЖивотного.НеСтельная;  
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;  
	
	СтельныеЖивотные = ВыборкаСтельныхЖивотных();
	Если СтельныеЖивотные.Количество() > 0 Тогда  
		Пока СтельныеЖивотные.Следующий() Цикл
			ТекстОшибки = СтрШаблон("Строка %1: животное %2 %3", СтельныеЖивотные.НомерСтроки,СтельныеЖивотные.Животное," стельное!") + Символы.ПС;
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = ТекстОшибки; 
			Сообщение.Поле = "Животные["+Строка(СтельныеЖивотные.НомерСтроки-1)+"].Животное";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЦикла;
		Отказ = Истина;  
	КонецЕсли; 
	
КонецПроцедуры

Функция ВыборкаЖивотных()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СинхронизацияПоловойОхотыЖивотные.Животное КАК Животное
	|ПОМЕСТИТЬ втЖивотные
	|ИЗ
	|	Документ.СинхронизацияПоловойОхоты.Животные КАК СинхронизацияПоловойОхотыЖивотные
	|ГДЕ
	|	СинхронизацияПоловойОхотыЖивотные.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЖивотные.Животное КАК Животное,
	|	СостояниеЖивотныхСрезПоследних.СостояниеЖивотного КАК СостояниеЖивотного
	|ИЗ
	|	втЖивотные КАК втЖивотные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеЖивотных.СрезПоследних(&ДатаДокумента, Регистратор <> &Ссылка) КАК СостояниеЖивотныхСрезПоследних
	|		ПО втЖивотные.Животное = СостояниеЖивотныхСрезПоследних.Животное";
	Запрос.УстановитьПараметр("ДатаДокумента",Дата);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;

КонецФункции

Функция ВыборкаСтельныхЖивотных()
	
	ТЧЖивотные = Животные.Выгрузить(,"НомерСтроки, Животное");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧЖивотные.НомерСтроки КАК НомерСтроки,
	|	ТЧЖивотные.Животное КАК Животное
	|ПОМЕСТИТЬ втЖивотные
	|ИЗ
	|	&ТЧЖивотные КАК ТЧЖивотные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЖивотные.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЖивотные.Животное) КАК Животное,
	|	СостояниеЖивотныхСрезПоследних.СостояниеЖивотного КАК СостояниеЖивотного
	|ИЗ
	|	втЖивотные КАК втЖивотные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеЖивотных.СрезПоследних(&ДатаДокумента, ) КАК СостояниеЖивотныхСрезПоследних
	|		ПО втЖивотные.Животное = СостояниеЖивотныхСрезПоследних.Животное
	|ГДЕ
	|	СостояниеЖивотныхСрезПоследних.СостояниеЖивотного = ЗНАЧЕНИЕ(Перечисление.СтатусЖивотного.Стельная)
	|	И СостояниеЖивотныхСрезПоследних.Регистратор <> &Регистратор";
	Запрос.УстановитьПараметр("ДатаДокумента",Дата);
	Запрос.УстановитьПараметр("ТЧЖивотные",ТЧЖивотные);
	Запрос.УстановитьПараметр("Регистратор",Ссылка);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Возврат Выборка;
	
КонецФункции // ПроверкаНаСтельность()



