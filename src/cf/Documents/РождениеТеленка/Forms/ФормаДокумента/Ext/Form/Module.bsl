&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;
		Объект.Ферма = Объект.Автор.ОсновнаяФерма;
		
	ИначеЕсли Объект.Проведен И НЕ РольДоступна("ПолныеПрава") Тогда
		
		ТолькоПросмотр = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Документ уже проведен, открывается в режиме только просмотра";
		Сообщение.Сообщить();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОбщегоНазначения.ТекущийПользователь() <> Объект.Автор Тогда
		Элементы.Автор.Доступность = Ложь
	КонецЕсли;
	
	ПроверкаАвтонумерацииФермы();
	
	Если НЕ ТолькоПросмотр Тогда
		ТолькоПросмотр = ОграничениеДоступности.ОграничитьДанныеДокумента(Объект.Дата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеМатьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Животные.ТекущиеДанные;
	
	//ПРОВЕРИМ МОЖЕТ ЛИ МАМА БЫТЬ ДОБАВЛЕНА В ЭТОТ ДОКУМЕНТ
	ДатаПоследнегоОсемения 	= ДанныеЖивотных.ДатаПоследнегоОсеменения(ТекущиеДанные.Мать, Объект.Дата);
	ДатаПоследнегоАборта	= ДанныеЖивотных.ДатаПоследнегоАборта(ТекущиеДанные.Мать, Объект.Дата, Истина);
	ДатаПоследнегоОтела		= ДанныеЖивотных.ДатаПоследнегоОтела(ТекущиеДанные.Мать, Объект.Дата);
	ДнейСтельности 			= ДанныеЖивотных.ДнейСтельности(ТекущиеДанные.Мать, Объект.Дата);
	СтатусЖивотного			= ДанныеЖивотных.СтатусЖивотного(ТекущиеДанные.Мать, Объект.Дата);
	
	Если Не СтатусЖивотного = ПредопределенноеЗначение("Перечисление.СтатусЖивотного.Стельная") Тогда
		
		Сообщить("Животное не стельно, добавление в документ отел невозможно");
		ТекущиеДанные.Мать = "";
		Возврат;
	КонецЕсли;
	
	Если ДатаПоследнегоОсемения < ДатаПоследнегоОтела Тогда
		
		Сообщить("По данному животному нет данных об осеменениях");
		ТекущиеДанные.Мать = "";
		Возврат;
		
	КонецЕсли;
	
	Если ДатаПоследнегоОсемения < ДатаПоследнегоАборта Тогда
		
		Сообщить("У данного животного было прерывание стельности");
		ТекущиеДанные.Мать = "";
		Возврат;
		
	КонецЕсли;
	
	Если ДнейСтельности < 260 Тогда
		
		Сообщить("Период вынашивания меньше 260 дней. Используйте документ прерывания стельности");
		ТекущиеДанные.Мать = "";
		Возврат;
		
	КонецЕсли;		
		
	ТекущиеДанные.ДнейСтельности 	= ДнейСтельности;
	
	Если НЕ ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.ПустаяСсылка") Тогда
		
		ТекущиеДанные.КорпусРазмещения = ?(ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.Корова"), 
		              							Объект.Ферма.КорпусДляНоворожденныхТелок, 
										Объект.Ферма.КорпусДляНоворожденныхБычков);
	Иначе										
		
		ТекущиеДанные.КорпусРазмещения = "";
		
	КонецЕсли;
	
	ЗаполнитьКличкуТеленка();
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ПроверкаВозможностиРедактирования(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеПолТеленкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Животные.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.ПустаяСсылка") Тогда
		
		ТекущиеДанные.КорпусРазмещения = ?(ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.Корова"), 
		                                	ОбщегоНазначения.ПолучитьСвойствоОбъекта(Объект.Ферма, "КорпусДляНоворожденныхТелок"),
											ОбщегоНазначения.ПолучитьСвойствоОбъекта(Объект.Ферма, "КорпусДляНоворожденныхБычков"));
	КонецЕсли;

	
	ТекущиеДанные.КличкаТеленка = ?(ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.Корова") 
								И ЗначениеЗаполнено(ТекущиеДанные.Мать), 
								СокрЛП(ОбщегоНазначения.ПолучитьСвойствоОбъекта(ТекущиеДанные.Мать, "Кличка")), 
								"");
								
	ЗаполнитьКличкуТеленка();								
								
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиРедактирования(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ферма)  Тогда
			
			Отказ = Истина;
			Предупреждение("Для редактирования данных необходимо заполнить Ферму");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьКличкуТеленка()
	
	ТекущиеДанные = Элементы.Животные.ТекущиеДанные;
	
	Если ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.ПустаяСсылка") Тогда
		
		ТекущиеДанные.КличкаТеленка = "";
	ИначеЕсли ТекущиеДанные.ПолТеленка = ПредопределенноеЗначение("Перечисление.ПолЖивотного.Корова") Тогда
		
		ТекущиеДанные.КличкаТеленка = ОбщегоНазначения.ПолучитьСвойствоОбъекта(ТекущиеДанные.Мать, "Кличка");
		
	Иначе
		
		ТекущиеДанные.КличкаТеленка = "Бык";
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЖивотныеМатьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Ферма", Объект.Ферма);
	СписокПараметров.Вставить("Корпус", ОбщегоНазначения.ПолучитьСвойствоОбъекта(Объект.Ферма, "КорпусДляОтела"));
	СписокПараметров.Вставить("НеобходимоОповестить", Истина);
	
	ВзаимодействиеКлиент.ОткрытьФормуВыбораЖивотного(ЭтаФорма, СписокПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ТекущиеДанные = Элементы.Животные.ТекущиеДанные;
	
	ТекущиеДанные.Мать = ВыбранноеЗначение;
	
	ЖивотныеМатьПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеМатьАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", ДанныеЖивотных.ПолучитьСписокЖивотныхПоФермеДокумента(Объект.Ферма, Объект.Дата, ОбщегоНазначения.ПолучитьСвойствоОбъекта(Объект.Ферма, "КорпусДляОтела")));
	СписокПараметров = Новый Массив;
	СписокПараметров.Добавить(ПараметрВыбора);
	ФиксированныеПараметры = Новый ФиксированныйМассив(СписокПараметров);
	
	Элемент.ПараметрыВыбора = ФиксированныеПараметры;
	
КонецПроцедуры

&НаКлиенте
Процедура ФермаПриИзменении(Элемент)
	
	ПроверкаАвтонумерацииФермы();
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаАвтонумерацииФермы()
	
	ВключенаАвтонумерация = ОбщегоНазначения.ПолучитьСвойствоОбъекта(Объект.Ферма, "АвтонумерацияЖивотного");
	
	Элементы.Животные.ПодчиненныеЭлементы.ЖивотныеГруппа3.Видимость = Не ВключенаАвтонумерация;
	Элементы.Животные.ПодчиненныеЭлементы.Группа4.ПодчиненныеЭлементы.ЖивотныеКличкаТеленка.Видимость = Не ВключенаАвтонумерация;
	
КонецФункции


