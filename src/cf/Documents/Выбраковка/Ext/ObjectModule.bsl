Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ВыбраковкаЖивотных.Записывать 		= Истина;
	Движения.ПроизводственныеГруппы.Записывать 	= Истина;
	
	Для каждого СтрокаТабличнойЧасти Из Животные Цикл
		
		НовоеДвижение 					= Движения.ВыбраковкаЖивотных.Добавить();
		НовоеДвижение.Период   			= Дата;
		НовоеДвижение.Животное 			= СтрокаТабличнойЧасти.Животное;
		НовоеДвижение.Брак	 			= Истина;
		НовоеДвижение.Регистратор		= Ссылка; 
		НовоеДвижение.ПричинаВыбраковки	= СтрокаТабличнойЧасти.ПричинаВыбраковки; 
		
		НовоеДвижение 							= Движения.ПроизводственныеГруппы.Добавить();
		НовоеДвижение.Животное 					= СтрокаТабличнойЧасти.Животное;
		НовоеДвижение.Период 					= Дата;
		НовоеДвижение.ПроизводственнаяГруппа	= Перечисления.ПроизводственныеГруппы.Брак;  
		
	КонецЦикла;
	
КонецПроцедуры    

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВыбракованныеЖивотные = ВыборкаВыбракованныхЖивотных();
	Если ВыбракованныеЖивотные.Количество() > 0 Тогда  
		Пока ВыбракованныеЖивотные.Следующий() Цикл
			ТекстОшибки = "Строка " + ВыбракованныеЖивотные.НомерСтроки + ": животное " + ВыбракованныеЖивотные.Животное + " уже выбраковано документом: " + ВыбракованныеЖивотные.ДокументВыбраковки + Символы.ПС;
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = ТекстОшибки; 
			Сообщение.Поле = "Животные["+Строка(ВыбракованныеЖивотные.НомерСтроки-1)+"].Животное";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЦикла;
		Отказ = Истина; 	
	КонецЕсли;	
	
КонецПроцедуры  

Функция ВыборкаВыбракованныхЖивотных()
	
	ТЧЖивотные = Животные.Выгрузить(,"НомерСтроки, Животное");
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЧЖивотные.НомерСтроки КАК НомерСтроки,
	               |	ТЧЖивотные.Животное КАК Животное
	               |ПОМЕСТИТЬ втЖивотные
	               |ИЗ
	               |	&ТЧЖивотные КАК ТЧЖивотные
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЖивотные.НомерСтроки КАК НомерСтроки,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЖивотные.Животное) КАК Животное,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВыбраковкаЖивотныхСрезПоследних.Регистратор) КАК ДокументВыбраковки
	               |ИЗ
	               |	втЖивотные КАК втЖивотные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыбраковкаЖивотных.СрезПоследних(, Регистратор <> &Ссылка) КАК ВыбраковкаЖивотныхСрезПоследних
	               |		ПО втЖивотные.Животное = ВыбраковкаЖивотныхСрезПоследних.Животное";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("ДатаДокумента",МоментВремени());
	Запрос.УстановитьПараметр("ТЧЖивотные",ТЧЖивотные);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Возврат Выборка;

КонецФункции // ПроверкаНаСтельность()












































