
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.КонтрольныеНадои.Записывать = Истина;
	ВыборкаДанных = ПолучитьДанныеПоТЧ();	
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		НовоеДвижение = Движения.КонтрольныеНадои.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, ВыборкаДанных);
		НовоеДвижение.Период 		= Дата;
		НовоеДвижение.ТипНадоев 	= Перечисления.ТипНадоев.Контрольные;
		
	КонецЦикла;
	
КонецПроцедуры 

Функция ПолучитьДанныеПоТЧ()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтрольнаяДойкаЖивотные.Животное КАК Животное,
	|	КонтрольнаяДойкаЖивотные.УдойУтро КАК УдойУтро,
	|	КонтрольнаяДойкаЖивотные.УдойОбед КАК УдойОбед,
	|	КонтрольнаяДойкаЖивотные.УдойВечер КАК УдойВечер,
	|	КонтрольнаяДойкаЖивотные.УдойСуточный КАК УдойСуточный,
	|	КонтрольнаяДойкаЖивотные.ПрошлыйСреднесуточный КАК ПрошлыйСреднесуточный,
	|	КонтрольнаяДойкаЖивотные.Разница КАК Разница
	|ПОМЕСТИТЬ втЖивотныеТЧ
	|ИЗ
	|	Документ.КонтрольнаяДойка.Животные КАК КонтрольнаяДойкаЖивотные
	|ГДЕ
	|	КонтрольнаяДойкаЖивотные.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВложенныйЗапрос.КоличествоЛактаций) КАК НомерЛактации,
	|	МАКСИМУМ(ВложенныйЗапрос.ДатаЛактации) КАК ДатаЛактации,
	|	втЖивотныеТЧ.Животное КАК Животное,
	|	втЖивотныеТЧ.УдойУтро КАК УдойУтро,
	|	втЖивотныеТЧ.УдойОбед КАК УдойОбед,
	|	втЖивотныеТЧ.УдойВечер КАК УдойВечер,
	|	втЖивотныеТЧ.УдойСуточный КАК УдойСуточный,
	|	втЖивотныеТЧ.ПрошлыйСреднесуточный КАК ПрошлыйСреднесуточный,
	|	втЖивотныеТЧ.Разница КАК Разница,
	|	МАКСИМУМ(РАЗНОСТЬДАТ(ЕСТЬNULL(ВложенныйЗапрос.ДатаЛактации, &ДатаДокумента), &ДатаДокумента, ДЕНЬ)) КАК ДойныйДень
	|ИЗ
	|	втЖивотныеТЧ КАК втЖивотныеТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Отелы.Мать КАК Животное,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|					КОГДА Отелы.Регистратор ССЫЛКА Документ.ДобавлениеЖивотного
	|						ТОГДА Отелы.УИД
	|					ИНАЧЕ Отелы.Период
	|				КОНЕЦ) КАК КоличествоЛактаций,
	|			МАКСИМУМ(Отелы.Период) КАК ДатаЛактации
	|		ИЗ
	|			втЖивотныеТЧ КАК втЖивотныеТЧ
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Отелы КАК Отелы
	|				ПО втЖивотныеТЧ.Животное = Отелы.Мать
	|		ГДЕ
	|			Отелы.Период <= &ДатаДокумента
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Отелы.Мать
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			Аборты.Мать,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Аборты.Период),
	|			МАКСИМУМ(Аборты.Период)
	|		ИЗ
	|			втЖивотныеТЧ КАК втЖивотныеТЧ
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аборты КАК Аборты
	|				ПО втЖивотныеТЧ.Животное = Аборты.Мать
	|		ГДЕ
	|			Аборты.ОткрытьЛактацию = ИСТИНА
	|			И Аборты.Период <= &ДатаДокумента
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Аборты.Мать) КАК ВложенныйЗапрос
	|		ПО втЖивотныеТЧ.Животное = ВложенныйЗапрос.Животное
	|
	|СГРУППИРОВАТЬ ПО
	|	втЖивотныеТЧ.Животное,
	|	втЖивотныеТЧ.УдойУтро,
	|	втЖивотныеТЧ.УдойОбед,
	|	втЖивотныеТЧ.УдойВечер,
	|	втЖивотныеТЧ.УдойСуточный,
	|	втЖивотныеТЧ.ПрошлыйСреднесуточный,
	|	втЖивотныеТЧ.Разница"; 
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции













