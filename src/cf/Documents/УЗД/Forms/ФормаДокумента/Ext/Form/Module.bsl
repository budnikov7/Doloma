&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;
		Объект.Ферма = Объект.Автор.ОсновнаяФерма;
	Конецесли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеЖивотноеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Животные.ТекущиеДанные;
	
	СтруктураРеквизитов = ОбработкаНаСервере(ТекущиеДанные.Животное, ТекущиеДанные.Статус);
	
	Если НЕ СтруктураРеквизитов.Количество() Тогда
		ТекущиеДанные.Животное = "";
	КонецЕсли;
	
	Для каждого ЗначениеРеквизита Из СтруктураРеквизитов Цикл
		
		ТекущиеДанные[ЗначениеРеквизита.Ключ] = ЗначениеРеквизита.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеСтатусПриИзменении(Элемент)
	
	ЖивотныеЖивотноеПриИзменении(Неопределено);
	
КонецПроцедуры

&НаСервере
Функция ОбработкаНаСервере(Животное, СтатусЖивотного)
	
	СтруктураРеквизитов = Новый Соответствие;
	ПустаяДата = Дата('00010101');
	
	//ПОЛУЧИМ БЛИЖАЙЩУЮ ДАТУ К ДАТЕ ДОКУМЕНТА
	ДатаПоследнегоОсеменения 	= ДанныеЖивотных.ДатаПоследнегоОсеменения(Животное, Объект.Дата);
	ДатаПоследнегоОтела			= ДанныеЖивотных.ДатаПоследнегоОтела(Животное, Объект.Дата);
	
	Если ДатаПоследнегоОсеменения < ДатаПоследнегоОтела Тогда
		
		Сообщить("По даннной корове не было осеменения. Корова не нуждается в УЗИ");
		Возврат Новый Соответствие;
	КонецЕсли;
	
	//ПОЛУЧИМ КОЛИЧЕСТВО ДИАГНОСТИК С МОМЕНТА ОСЕМЕНЕНИЯ
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕстьNull(Сумма(1), 0) КАК КоличествоДиагностик
	|ИЗ
	|	РегистрСведений.УЗИисследования
	|ГДЕ
	|	Период >= &ДатаОсеменения
	|	И Период <= &ТекущаяДата
	|	И Мать = &МамаЖивотенка
	|	И Регистратор <> &ТекущийДокумент";
	
	Запрос.УстановитьПараметр("ТекущаяДата", 		Объект.Дата);
	Запрос.УстановитьПараметр("МамаЖивотенка", 		Животное);
	Запрос.УстановитьПараметр("ДатаОсеменения", 	ДатаПоследнегоОсеменения);
	Запрос.УстановитьПараметр("ТекущийДокумент", 	Объект.Ссылка);
	
	Ответ = Запрос.Выполнить();
	
	Если НЕ Ответ.Пустой() Тогда
		
		Результат = Ответ.Выгрузить().Получить(0).КоличествоДиагностик;
		
		СтруктураРеквизитов.Вставить("НомерУЗИ", Результат + 1);
	КонецЕсли;
	
	//ПОЛУЧИМ ДЕНЬ ДИАГНОСТИКИ
	РазностьДат 			= ОбщегоНазначения.РазностьДат(Объект.Дата, ДатаПоследнегоОсеменения);
	СтруктураРеквизитов.Вставить("ДеньУЗИ", РазностьДат);
	
	Если СтруктураРеквизитов["НомерУЗИ"] = 1 И СтатусЖивотного = Перечисления.СтатусЖивотного.Стельная Тогда 
		
		//ПОЛУЧИМ ДАТУ ПРЕДПОЛАГАЕМОГО ОТЕЛА
		СтруктураРеквизитов.Вставить("ДатаПредполагаемогоОтёла", 		ОбщегоНазначения.ДобавитьДни(ДатаПоследнегоОсеменения, 
																																				?(ДанныеЖивотных.ДатаПоследнегоОтела(Животное, Объект.Дата) = Дата(1, 1, 1), 
																																							Объект.Ферма.ДнейСтельностиТелки - 1, 
																																							Объект.Ферма.ДнейСтельностиКоровы - 1)));
		СтруктураРеквизитов.Вставить("ДатаЗапуска", 											ОбщегоНазначения.ДобавитьДни(СтруктураРеквизитов["ДатаПредполагаемогоОтёла"], - Объект.Ферма.ДнейЗапуска));
		СтруктураРеквизитов.Вставить("ДатаПереводаВоВторойСухостой", 	ОбщегоНазначения.ДобавитьДни(СтруктураРеквизитов["ДатаПредполагаемогоОтёла"], - Объект.Ферма.ДнейПереводаВСухостой));
		
	ИначеЕсли СтруктураРеквизитов["НомерУЗИ"] > 1 И ДанныеЖивотных.ДатаПоследнегоУЗИ(Животное, Объект.Дата) > ДатаПоследнегоОсеменения И СтатусЖивотного = Перечисления.СтатусЖивотного.Стельная Тогда
		
		СтруктураРеквизитов = ДанныеЖивотных.ДанныеПоследнегоУЗИ(Животное, Объект.Дата);
		СтруктураРеквизитов.Вставить("НомерУЗИ", ?(Результат = Неопределено, 1, Результат + 1)); 
		
	Иначе
		
		СтруктураРеквизитов.Вставить("ДатаПредполагаемогоОтёла", 			ПустаяДата);
		СтруктураРеквизитов.Вставить("ДатаЗапуска", 											ПустаяДата);
		СтруктураРеквизитов.Вставить("ДатаПереводаВоВторойСухостой", 	ПустаяДата);
		
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

&НаКлиенте
Процедура ЖивотныеЖивотноеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Ферма", Объект.Ферма);
	СписокПараметров.Вставить("НеобходимоОповестить", Истина);
	
	ВзаимодействиеКлиент.ОткрытьФормуВыбораЖивотного(ЭтаФорма, СписокПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖивотныеЖивотноеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", ДанныеЖивотных.ПолучитьСписокЖивотныхПоФермеДокумента(Объект.Ферма, Объект.Дата));
	СписокПараметров = Новый Массив;
	СписокПараметров.Добавить(ПараметрВыбора);
	ФиксированныеПараметры = Новый ФиксированныйМассив(СписокПараметров);
	
	Элемент.ПараметрыВыбора = ФиксированныеПараметры;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ТекущиеДанные = Элементы.Животные.ТекущиеДанные;
	
	ТекущиеДанные.Животное = ВыбранноеЗначение;
	
	ЖивотныеЖивотноеПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтаФорма.ТолькоПросмотр = ОграничениеДоступности.ОграничитьДанныеДокумента(Объект.Дата);
	
КонецПроцедуры
