&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДата();
	КонецЕсли;
	ЭтаФорма.ТолькоПросмотр = ОграничениеДоступности.ОграничитьДанныеДокумента(Объект.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЦены(Команда)
	РассчитатьЦеныНаСервере();
КонецПроцедуры

&НаСервере
Процедура РассчитатьЦеныНаСервере()
	
	ТЗНоменклатура = Новый ТаблицаЗначений;
	ТЗНоменклатура.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Для каждого Строчка Из Объект.Корма Цикл
		Если ЗначениеЗаполнено(Строчка.Номенклатура) Тогда
			НоваяСтрочка = ТЗНоменклатура.Добавить();	
			НоваяСтрочка.Номенклатура = Строчка.Номенклатура;
		КонецЕсли;
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗНоменклатура.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	&ТЗНоменклатура КАК ТЗНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТоварыНаСкладахОстатки.КоличествоОстаток = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТоварыНаСкладахОстатки.СуммаОстаток / ТоварыНаСкладахОстатки.КоличествоОстаток
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	втНоменклатура КАК втНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(&Дата, ) КАК ТоварыНаСкладахОстатки
	|		ПО втНоменклатура.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура";  
	Запрос.УстановитьПараметр("ТЗНоменклатура", ТЗНоменклатура);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрОтбора = Новый Структура("Номенклатура", Выборка.Номенклатура);
		НайденныеСтроки = Объект.Корма.НайтиСтроки(СтрОтбора);
		Для каждого Строчка Из НайденныеСтроки Цикл
			Строчка.Цена = Выборка.Цена;	
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТекущуюСтроку(Команда) 
	
	ЗаполнитьТекущуюСтрокуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекущуюСтрокуНаСервере()
	
	ТекСтрока = Элементы.Корма.ТекущаяСтрока;
	СтрокаТЧ = Объект.Корма.НайтиПоИдентификатору(ТекСтрока);
	
	Если СтрокаТЧ = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ТаблицаПитательностиКормовСрезПоследних.СухоеВещество КАК СухоеВещество,
	|	ТаблицаПитательностиКормовСрезПоследних.ОЭ КАК ОЭ,
	|	ТаблицаПитательностиКормовСрезПоследних.XP КАК XP,
	|	ТаблицаПитательностиКормовСрезПоследних.nXp КАК nXp,
	|	ТаблицаПитательностиКормовСрезПоследних.RNB КАК RNB,
	|	ТаблицаПитательностиКормовСрезПоследних.СК КАК СК,
	|	ТаблицаПитательностиКормовСрезПоследних.СЖ КАК СЖ,
	|	ТаблицаПитательностиКормовСрезПоследних.СЗ КАК СЗ,
	|	ТаблицаПитательностиКормовСрезПоследних.Крахмал КАК Крахмал,
	|	ТаблицаПитательностиКормовСрезПоследних.ТранзитныйКрахмал КАК ТранзитныйКрахмал,
	|	ТаблицаПитательностиКормовСрезПоследних.Сахар КАК Сахар,
	|	ТаблицаПитательностиКормовСрезПоследних.NDF КАК NDF,
	|	ТаблицаПитательностиКормовСрезПоследних.ADF КАК ADF,
	|	ТаблицаПитательностиКормовСрезПоследних.NEL КАК NEL,
	|	ТаблицаПитательностиКормовСрезПоследних.Ca КАК Ca,
	|	ТаблицаПитательностиКормовСрезПоследних.Na КАК Na,
	|	ТаблицаПитательностиКормовСрезПоследних.Mg КАК Mg,
	|	ТаблицаПитательностиКормовСрезПоследних.K КАК K,
	|	ТаблицаПитательностиКормовСрезПоследних.Cl КАК Cl,
	|	ТаблицаПитательностиКормовСрезПоследних.S КАК S,
	|	ТаблицаПитательностиКормовСрезПоследних.P КАК P,
	|	ТаблицаПитательностиКормовСрезПоследних.Структ КАК Структ,
	|	ТаблицаПитательностиКормовСрезПоследних.СтрКл КАК СтрКл,
	|	ТаблицаПитательностиКормовСрезПоследних.РастворимыйСП КАК РастворимыйСП,
	|	ТаблицаПитательностиКормовСрезПоследних.UDP КАК UDP,
	|	ТаблицаПитательностиКормовСрезПоследних.ADL КАК ADL,
	|	ТаблицаПитательностиКормовСрезПоследних.УксуснаяКислота КАК УксуснаяКислота,
	|	ТаблицаПитательностиКормовСрезПоследних.МолочнаяКислота КАК МолочнаяКислота,
	|	ТаблицаПитательностиКормовСрезПоследних.pH КАК pH,
	|	ТаблицаПитательностиКормовСрезПоследних.ПереваримоеОрганическоеВещество КАК ПереваримоеОрганическоеВещество
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаблицаПитательностиКормов.СрезПоследних(, Номенклатура = &Номенклатура) КАК ТаблицаПитательностиКормовСрезПоследних
	|		ПО (ТаблицаПитательностиКормовСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
	|ГДЕ
	|	СправочникНоменклатура.Ссылка = &Номенклатура"; 
	Запрос.УстановитьПараметр("Номенклатура", СтрокаТЧ.Номенклатура);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
	
	КонецЕсли;
	

КонецПроцедуры // ЗаполнитьПоСправочникуНоменклатурыНаСервере()

&НаКлиенте
Процедура ЗаполнитьПоСправочникуНоменклатуры(Команда)

	ЗаполнитьПоСправочникуНоменклатурыНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьПоСправочникуНоменклатурыНаСервере()
	
	Объект.Корма.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ТаблицаПитательностиКормовСрезПоследних.СухоеВещество КАК СухоеВещество,
	|	ТаблицаПитательностиКормовСрезПоследних.ОЭ КАК ОЭ,
	|	ТаблицаПитательностиКормовСрезПоследних.XP КАК XP,
	|	ТаблицаПитательностиКормовСрезПоследних.nXp КАК nXp,
	|	ТаблицаПитательностиКормовСрезПоследних.RNB КАК RNB,
	|	ТаблицаПитательностиКормовСрезПоследних.СК КАК СК,
	|	ТаблицаПитательностиКормовСрезПоследних.СЖ КАК СЖ,
	|	ТаблицаПитательностиКормовСрезПоследних.СЗ КАК СЗ,
	|	ТаблицаПитательностиКормовСрезПоследних.Крахмал КАК Крахмал,
	|	ТаблицаПитательностиКормовСрезПоследних.ТранзитныйКрахмал КАК ТранзитныйКрахмал,
	|	ТаблицаПитательностиКормовСрезПоследних.Сахар КАК Сахар,
	|	ТаблицаПитательностиКормовСрезПоследних.NDF КАК NDF,
	|	ТаблицаПитательностиКормовСрезПоследних.ADF КАК ADF,
	|	ТаблицаПитательностиКормовСрезПоследних.NEL КАК NEL,
	|	ТаблицаПитательностиКормовСрезПоследних.Ca КАК Ca,
	|	ТаблицаПитательностиКормовСрезПоследних.Na КАК Na,
	|	ТаблицаПитательностиКормовСрезПоследних.Mg КАК Mg,
	|	ТаблицаПитательностиКормовСрезПоследних.K КАК K,
	|	ТаблицаПитательностиКормовСрезПоследних.Cl КАК Cl,
	|	ТаблицаПитательностиКормовСрезПоследних.S КАК S,
	|	ТаблицаПитательностиКормовСрезПоследних.P КАК P,
	|	ТаблицаПитательностиКормовСрезПоследних.Структ КАК Структ,
	|	ТаблицаПитательностиКормовСрезПоследних.СтрКл КАК СтрКл,
	|	ТаблицаПитательностиКормовСрезПоследних.РастворимыйСП КАК РастворимыйСП,
	|	ТаблицаПитательностиКормовСрезПоследних.UDP КАК UDP,
	|	ТаблицаПитательностиКормовСрезПоследних.ADL КАК ADL,
	|	ТаблицаПитательностиКормовСрезПоследних.УксуснаяКислота КАК УксуснаяКислота,
	|	ТаблицаПитательностиКормовСрезПоследних.МолочнаяКислота КАК МолочнаяКислота,
	|	ТаблицаПитательностиКормовСрезПоследних.pH КАК pH,
	|	ТаблицаПитательностиКормовСрезПоследних.ПереваримоеОрганическоеВещество КАК ПереваримоеОрганическоеВещество
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаблицаПитательностиКормов.СрезПоследних КАК ТаблицаПитательностиКормовСрезПоследних
	|		ПО (ТаблицаПитательностиКормовСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
	|ГДЕ
	|	СправочникНоменклатура.ПометкаУдаления = ЛОЖЬ
	|	И СправочникНоменклатура.ВидКорма <> ЗНАЧЕНИЕ(Перечисление.ВидыКормов.ПустаяСсылка)
	|	И СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Корм)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Корма.Добавить();	
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	
	КонецЦикла;
	

КонецПроцедуры // ЗаполнитьПоСправочникуНоменклатурыНаСервере()

