
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыНаСкладах.Записывать 		= Истина;
	Движения.ВзаиморасчетыСКлиентами.Записывать = Истина; 
	Движения.РасчетыПоДоговорам.Записывать 		= Истина;
		
	Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
		
		Количество = Справочники.Номенклатура.РассчитатьКоличествоВОсновнойЕдиницеИзмерения(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ЕдиницаИзмерения, СтрокаТабличнойЧасти.Количество);
		Сумма = СтрокаТабличнойЧасти.Сумма;
		Если СтрокаТабличнойЧасти.СуммаСНДС <> 0 Тогда
			Сумма 	= СтрокаТабличнойЧасти.СуммаСНДС;
		КонецЕсли;
		
		Если СтрокаТабличнойЧасти.Номенклатура.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
			НовоеДвижение 				= Движения.ТоварыНаСкладах.Добавить();
			НовоеДвижение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
			НовоеДвижение.Период 		= Дата;
			НовоеДвижение.Склад 		= Склад;
			НовоеДвижение.Номенклатура 	= СтрокаТабличнойЧасти.Номенклатура;
			НовоеДвижение.Количество 	= Количество; 
			НовоеДвижение.Сумма         = Сумма;
			НовоеДвижение.Регистратор	= Ссылка;
		КонецЕсли;
		
		НовоеДвижение 				= Движения.ВзаиморасчетыСКлиентами.Добавить();
		НовоеДвижение.Период 		= Дата;
		НовоеДвижение.Регистратор 	= Ссылка;
		НовоеДвижение.Контрагент 	= Контрагент;
		НовоеДвижение.Номенклатура 	= СтрокаТабличнойЧасти.Номенклатура;
		НовоеДвижение.СтатьяРасхода	= СтатьяРасхода;
		НовоеДвижение.Сумма         = Сумма;
		НовоеДвижение.Цена			= СтрокаТабличнойЧасти.Цена;
		НовоеДвижение.Количество	= Количество;   
		
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			НовоеДвижение 				= Движения.РасчетыПоДоговорам.Добавить();
			НовоеДвижение.Период 		= Дата;
			НовоеДвижение.Регистратор 	= Ссылка;
			НовоеДвижение.Контрагент 	= Контрагент;
			НовоеДвижение.Договор 		= ДоговорКонтрагента;
			НовоеДвижение.Номенклатура 	= СтрокаТабличнойЧасти.Номенклатура;
			НовоеДвижение.Количество 	= Количество;
			НовоеДвижение.Сумма         = Сумма;
			НовоеДвижение.Цена			= СтрокаТабличнойЧасти.Цена;
		КонецЕсли;
		
	КонецЦикла; 
	
	Движения.Записать();

	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ПроверитьРасчетыПоДоговору(Отказ); 
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Индекс = 0 по Продукция.Количество()-1 Цикл
		СтрокаТовар = Продукция.Получить(Индекс);
		Если Не ЗначениеЗаполнено(СтрокаТовар.ЕдиницаИзмерения) И СтрокаТовар.Номенклатура.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "В строке " + (Индекс + 1) + " не заполнена единица измерения";
			Сообщение.Поле = "Продукция[" + Индекс + "].ЕдиницаИзмерения";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТовар.СтавкаНДС) И УчитыватьНДС Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "В строке " + (Индекс + 1) + " не заполнена ставка НДС";
			Сообщение.Поле = "Продукция[" + Индекс + "].СтавкаНДС";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;	
	КонецЦикла;	 

	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ПроверитьЦеныПоДоговору(Отказ); 
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПроверитьРасчетыПоДоговору(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорКонтрагентаТовары.Номенклатура КАК Номенклатура,
	|	ДоговорКонтрагентаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ДоговорКонтрагентаТовары.Количество) КАК Количество,
	|	СУММА(ДоговорКонтрагентаТовары.Сумма) КАК Сумма
	|ПОМЕСТИТЬ вт_ТоварыПоДоговору
	|ИЗ
	|	Справочник.ДоговорКонтрагента.Товары КАК ДоговорКонтрагентаТовары
	|ГДЕ
	|	ДоговорКонтрагентаТовары.Ссылка = &Договор
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоговорКонтрагентаТовары.Номенклатура,
	|	ДоговорКонтрагентаТовары.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ТоварыПоДоговору.Номенклатура КАК Номенклатура,
	|	вт_ТоварыПоДоговору.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	вт_ТоварыПоДоговору.Количество КАК Количество,
	|	вт_ТоварыПоДоговору.Сумма КАК Сумма,
	|	ЕСТЬNULL(НоменклатураЕдиницыИзмерения.Коэффициент, 1) КАК Коэффициент
	|ПОМЕСТИТЬ вт_ТоварыСКоэффициентами
	|ИЗ
	|	вт_ТоварыПоДоговору КАК вт_ТоварыПоДоговору
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ЕдиницыИзмерения КАК НоменклатураЕдиницыИзмерения
	|		ПО вт_ТоварыПоДоговору.ЕдиницаИзмерения = НоменклатураЕдиницыИзмерения.ЕдиницаИзмерения
	|			И вт_ТоварыПоДоговору.Номенклатура = НоменклатураЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоДоговорамОбороты.Номенклатура КАК Номенклатура,
	|	СУММА(РасчетыПоДоговорамОбороты.СуммаОборот) КАК Сумма,
	|	СУММА(РасчетыПоДоговорамОбороты.КоличествоОборот) КАК Количество
	|ПОМЕСТИТЬ вт_ДанныеРегистра
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорам.Обороты(
	|			,
	|			,
	|			,
	|			Контрагент = &Контрагент
	|				И Договор = &Договор) КАК РасчетыПоДоговорамОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоДоговорамОбороты.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДанныеРегистра.Номенклатура КАК Номенклатура,
	|	вт_ТоварыСКоэффициентами.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(вт_ТоварыСКоэффициентами.Количество, 0) КАК КоличествоПоДоговору,
	|	ЕСТЬNULL(вт_ТоварыСКоэффициентами.Сумма, 0) КАК СуммаПоДоговору,
	|	вт_ДанныеРегистра.Количество / ЕСТЬNULL(вт_ТоварыСКоэффициентами.Коэффициент, 1) КАК КоличествоВсего,
	|	вт_ДанныеРегистра.Сумма КАК СуммаВсего
	|ИЗ
	|	вт_ДанныеРегистра КАК вт_ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ТоварыСКоэффициентами КАК вт_ТоварыСКоэффициентами
	|		ПО (вт_ТоварыСКоэффициентами.Номенклатура = вт_ДанныеРегистра.Номенклатура)
	|ГДЕ
	|	(ЕСТЬNULL(вт_ТоварыСКоэффициентами.Количество, 0) * ЕСТЬNULL(вт_ТоварыСКоэффициентами.Коэффициент, 1) < вт_ДанныеРегистра.Количество
	|			ИЛИ ЕСТЬNULL(вт_ТоварыСКоэффициентами.Сумма, 0) < вт_ДанныеРегистра.Сумма)"; 
	Запрос.УстановитьПараметр("Ссылка", 	Ссылка);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Договор", 	ДоговорКонтрагента);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = СтрШаблон("По номенклатуре %1 превышена товарная спецификация: 
										|по договору количество %2 %3 на сумму %4 руб., текущий расход количество %5 %3 на сумму %6 руб.",
										Выборка.Номенклатура, Выборка.КоличествоПоДоговору, 
										Выборка.ЕдиницаИзмерения, Выборка.СуммаПоДоговору, 
										Выборка.КоличествоВсего, Выборка.СуммаВсего);
			Сообщение.Поле = "Товары";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЦеныПоДоговору(Отказ)
	
	ПроверятьЦены = Константы.ПроверятьЦеныПоДоговору.Получить();
	Если НЕ ПроверятьЦены Тогда
		Возврат;
	КонецЕсли;
	
	ТЗТовары = Продукция.Выгрузить(,"НомерСтроки, Номенклатура, Цена");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорКонтрагентаТовары.Номенклатура КАК Номенклатура,
	|	ДоговорКонтрагентаТовары.Цена КАК Цена
	|ПОМЕСТИТЬ вт_ТоварыПоДоговору
	|ИЗ
	|	Справочник.ДоговорКонтрагента.Товары КАК ДоговорКонтрагентаТовары
	|ГДЕ
	|	ДоговорКонтрагентаТовары.Ссылка = &Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗТовары.Номенклатура КАК Номенклатура,
	|	ТЗТовары.Цена КАК Цена,
	|	ТЗТовары.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ вт_ТоварыДокумента
	|ИЗ
	|	&ТЗТовары КАК ТЗТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	вт_ТоварыДокумента.Цена КАК ЦенаВДокументе,
	|	ЕСТЬNULL(вт_ТоварыПоДоговору.Цена, 0) КАК ЦенаПоДоговору,
	|	вт_ТоварыДокумента.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	вт_ТоварыДокумента КАК вт_ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ТоварыПоДоговору КАК вт_ТоварыПоДоговору
	|		ПО вт_ТоварыДокумента.Номенклатура = вт_ТоварыПоДоговору.Номенклатура
	|ГДЕ
	|	вт_ТоварыДокумента.Цена <> ЕСТЬNULL(вт_ТоварыПоДоговору.Цена, 0)"; 
	Запрос.УстановитьПараметр("ТЗТовары", 	ТЗТовары);
	Запрос.УстановитьПараметр("Договор", 	ДоговорКонтрагента);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстОшибки = СтрШаблон("По номенклатуре %1 цена %2 руб. не соответствует цене в договоре %3 руб.",
										Выборка.Номенклатура, Выборка.ЦенаВДокументе, Выборка.ЦенаПоДоговору);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"Продукция["+Строка(Выборка.НомерСтроки-1)+"].Цена","Объект",Отказ);
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры







