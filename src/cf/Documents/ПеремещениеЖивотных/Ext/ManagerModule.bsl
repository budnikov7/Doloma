
Процедура Печать(ТабДок, Ссылка) Экспорт

	Макет = Документы.ПеремещениеЖивотных.ПолучитьМакет("ОписьЖивотных");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеЖивотныхЖивотныеМеждуКорпусами.Животное КАК Животное,
	|	ПеремещениеЖивотныхЖивотныеМеждуКорпусами.Ссылка КАК Документ
	|ПОМЕСТИТЬ вт_Животные
	|ИЗ
	|	Документ.ПеремещениеЖивотных.ЖивотныеМеждуКорпусами КАК ПеремещениеЖивотныхЖивотныеМеждуКорпусами
	|ГДЕ
	|	ПеремещениеЖивотныхЖивотныеМеждуКорпусами.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеремещениеЖивотныхЖивотныеМеждуФермами.Животное,
	|	ПеремещениеЖивотныхЖивотныеМеждуФермами.Ссылка
	|ИЗ
	|	Документ.ПеремещениеЖивотных.ЖивотныеМеждуФермами КАК ПеремещениеЖивотныхЖивотныеМеждуФермами
	|ГДЕ
	|	ПеремещениеЖивотныхЖивотныеМеждуФермами.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеремещениеЖивотныхЖивотныеЗапуск.Животное,
	|	ПеремещениеЖивотныхЖивотныеЗапуск.Ссылка
	|ИЗ
	|	Документ.ПеремещениеЖивотных.ЖивотныеЗапуск КАК ПеремещениеЖивотныхЖивотныеЗапуск
	|ГДЕ
	|	ПеремещениеЖивотныхЖивотныеЗапуск.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Животные.Животное КАК Животное,
	|	МАКСИМУМ(Осеменения.Период) КАК ДатаОсеменения,
	|	вт_Животные.Документ КАК Документ
	|ПОМЕСТИТЬ вт_ЖивотныеСДатами
	|ИЗ
	|	вт_Животные КАК вт_Животные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Осеменения КАК Осеменения
	|		ПО вт_Животные.Животное = Осеменения.Животное
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Животные.Животное,
	|	вт_Животные.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ЖивотныеСДатами.Животное КАК Животное,
	|	вт_ЖивотныеСДатами.ДатаОсеменения КАК ДатаПоследнегоОсеменения,
	|	Осеменения.Семя КАК БыкПоследнегоОсеменения,
	|	вт_ЖивотныеСДатами.Животное.Код КАК НомерЖивотного,
	|	вт_ЖивотныеСДатами.Животное.БиркаХорриот КАК БиркаХорриот,
	|	вт_ЖивотныеСДатами.Животное.Родословная.Отец КАК Отец,
	|	вт_ЖивотныеСДатами.Животное.Родословная.Мать КАК Мать,
	|	ИнформацияОРождении.Период КАК ДатаРождения,
	|	СостояниеЖивотныхСрезПоследних.СостояниеЖивотного КАК Статус,
	|	ДатаПредполагаемогоОтёлаСрезПоследних.ДатаПредполагаемогоОтёла КАК ДатаПредполагаемогоОтёла,
	|	вт_ЖивотныеСДатами.Документ КАК Документ,
	|	вт_ЖивотныеСДатами.Документ.Номер КАК НомерДокумента,
	|	вт_ЖивотныеСДатами.Документ.Дата КАК ДатаДокумента
	|ИЗ
	|	вт_ЖивотныеСДатами КАК вт_ЖивотныеСДатами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Осеменения КАК Осеменения
	|		ПО вт_ЖивотныеСДатами.Животное = Осеменения.Животное
	|			И вт_ЖивотныеСДатами.ДатаОсеменения = Осеменения.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеЖивотных.СрезПоследних КАК СостояниеЖивотныхСрезПоследних
	|		ПО вт_ЖивотныеСДатами.Животное = СостояниеЖивотныхСрезПоследних.Животное
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатаПредполагаемогоОтёла.СрезПоследних КАК ДатаПредполагаемогоОтёлаСрезПоследних
	|		ПО вт_ЖивотныеСДатами.Животное = ДатаПредполагаемогоОтёлаСрезПоследних.Животное
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияОРождении КАК ИнформацияОРождении
	|		ПО вт_ЖивотныеСДатами.Животное = ИнформацияОРождении.Животное
	|ИТОГИ
	|	МАКСИМУМ(НомерДокумента),
	|	МАКСИМУМ(ДатаДокумента)
	|ПО
	|	Документ";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЖивотныеЗапускШапка = Макет.ПолучитьОбласть("ЖивотныеЗапускШапка");
	ОбластьЖивотныеЗапуск = Макет.ПолучитьОбласть("ЖивотныеЗапуск");
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьЗаголовок.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьЗаголовок);

		ТабДок.Вывести(ОбластьЖивотныеЗапускШапка);
		ВыборкаЖивотныеЗапуск = Выборка.Выбрать();
		Сч = 1;
		Пока ВыборкаЖивотныеЗапуск.Следующий() Цикл
			ОбластьЖивотныеЗапуск.Параметры.Заполнить(ВыборкаЖивотныеЗапуск); 
			ОбластьЖивотныеЗапуск.Параметры.НомерСтроки = Сч;
			Сч = Сч + 1;
			ТабДок.Вывести(ОбластьЖивотныеЗапуск, ВыборкаЖивотныеЗапуск.Уровень());
		КонецЦикла;

		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;

КонецПроцедуры
