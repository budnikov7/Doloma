
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)  
	
    СтандартнаяОбработка = Ложь;
    Настройки = КомпоновщикНастроек.ПолучитьНастройки();
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки); 
	
    ПараметрПериод = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета"));
    НачальнаяДата = ПараметрПериод.Значение.ДатаНачала;
    КонечнаяДата = ПараметрПериод.Значение.ДатаОкончания;   
	
    ТаблицаЦиклов = ПолучитьТаблицуЦиклов(НачальнаяДата,КонечнаяДата);
	Запрос = Новый Запрос;  
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТаблицаЦиклов ИЗ &ТаблицаЦиклов КАК ТаблицаЦиклов";
	Запрос.УстановитьПараметр("ТаблицаЦиклов", ТаблицаЦиклов);
	Запрос.Выполнить();    
	
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,ДанныеРасшифровки,,,МВТ);
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Ячейка = ДокументРезультат.Область(1,1);
	Если Ячейка.Текст = "Ферма" Тогда
		ДокументРезультат.ФиксацияСлева = 2;
	Иначе 
		Если ДокументРезультат.Рисунки.Количество() = 0 Тогда
			ДокументРезультат.ФиксацияСлева = 1;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры 

Функция ПолучитьТаблицуЦиклов(НачальнаяДата,КонечнаяДата)

	ТаблицаЦиклов = Новый ТаблицаЗначений;   
	КвДаты = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
    ТаблицаЦиклов.Колонки.Добавить("ДатаНачалаЦикла",Новый ОписаниеТипов("Дата",,,,,КвДаты));
    ТаблицаЦиклов.Колонки.Добавить("ДатаОкончанияЦикла",Новый ОписаниеТипов("Дата",,,,,КвДаты)); 
	ДатаНачалаЦикла 	= НачальнаяДата;
	ДатаОкончанияЦикла 	= НачальнаяДата;  
	Пока ДатаОкончанияЦикла < НачалоДня(КонечнаяДата) Цикл 
		ДатаОкончанияЦикла = КонецДня(ДатаНачалаЦикла + 1728000); //20 дней
        НоваяСтрока = ТаблицаЦиклов.Добавить();
        НоваяСтрока.ДатаНачалаЦикла 	= ДатаНачалаЦикла;
        НоваяСтрока.ДатаОкончанияЦикла 	= ДатаОкончанияЦикла; 
		ДатаНачалаЦикла = НачалоДня(ДатаОкончанияЦикла + 86400);
	КонецЦикла; 
	
	Возврат ТаблицаЦиклов;
	
КонецФункции // ПолучитьТаблицуЦиклов()

