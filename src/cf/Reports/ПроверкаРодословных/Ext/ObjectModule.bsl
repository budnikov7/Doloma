
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
    СтандартнаяОбработка = Ложь;
    Настройки = КомпоновщикНастроек.ПолучитьНастройки();
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки); 
	
    ТаблицаРядов = СформироватьТаблицуРядовСоСвязями();
	Запрос = Новый Запрос;  
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТаблицаРядов ИЗ &ТаблицаРядов КАК ТаблицаРядов";
	Запрос.УстановитьПараметр("ТаблицаРядов", ТаблицаРядов);
	Запрос.Выполнить();    
	
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,ДанныеРасшифровки,,,МВТ);
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

Функция СформироватьТаблицуРядовСоСвязями()

	МассивБазовый = Новый Массив; 
	МассивБазовый.Добавить("М"); 
	МассивБазовый.Добавить("О");
	
	МассивРядов = Новый Массив;
	МассивРядов.Добавить(МассивБазовый);
	
	ТаблицаРядов = Новый ТаблицаЗначений;
	ТаблицаРядов.Колонки.Добавить("Ряд", Новый ОписаниеТипов("Число"));
	ТаблицаРядов.Колонки.Добавить("ТипСвязи", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(20,ДопустимаяДлина.Переменная))); 
	
	Для Ряд = 1 По 3 Цикл    
		МассивПредыдущегоРяда = МассивРядов[Ряд - 1];
		МассивТекущегоРяда = Новый Массив; 
		
		Для каждого ТипСвязи Из МассивПредыдущегоРяда Цикл 
			Для каждого БазоваяТипСвязи Из МассивБазовый Цикл   
				МассивТекущегоРяда.Добавить(БазоваяТипСвязи + ТипСвязи);
			КонецЦикла;
		КонецЦикла;
		
		МассивРядов.Добавить(МассивТекущегоРяда);
	КонецЦикла;
	
	Сч = 1;
	Для каждого МассивРяда Из МассивРядов Цикл
		
		Для каждого Элемент Из МассивРяда Цикл
		
			НоваяСтрока = ТаблицаРядов.Добавить();
			НоваяСтрока.Ряд = Сч;
			НоваяСтрока.ТипСвязи = Элемент;
		
		КонецЦикла;
		Сч = Сч + 1;
		
	КонецЦикла; 
	
	Возврат ТаблицаРядов; 

КонецФункции
