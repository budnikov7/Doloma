
Процедура ОбработатьТЧ(ТЧ,СтруктураДействий) Экспорт
	
	Для Каждого СтрТабл Из ТЧ Цикл
		ОбработатьСтрокуТЧ(СтрТабл, СтруктураДействий);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий) Экспорт

	ЗаполнитьЕдиницуИзмеренияВСтрокеТЧ(                     ТекущаяСтрока, СтруктураДействий);
	ЗаполнитьСтавкуНДСВСтрокеТЧ(                            ТекущаяСтрока, СтруктураДействий);
	ЗаполнитьСреднююЦенуВСтрокеТЧ(                     		ТекущаяСтрока, СтруктураДействий); 
	ЗаполнитьЦенуПоДоговоруВСтрокеТЧ(                     	ТекущаяСтрока, СтруктураДействий); 
	ПересчитатьЦенуСНДС(                                    ТекущаяСтрока, СтруктураДействий);
	ПересчитатьСуммуВСтрокеТЧ(								ТекущаяСтрока, СтруктураДействий);
	ПересчитатьЦенуПоСуммеВСтрокеТЧ(						ТекущаяСтрока, СтруктураДействий);
	ПересчитатьСуммуСУчетомПогрешностиОкругленияВСтрокеТЧ(  ТекущаяСтрока, СтруктураДействий);
	ПересчитатьСуммуНДСВСтрокеТЧ(                           ТекущаяСтрока, СтруктураДействий);
	ПересчитатьСуммуСНДСВСтрокеТЧ(							ТекущаяСтрока, СтруктураДействий); 
	
КонецПроцедуры 

Процедура ЗаполнитьЕдиницуИзмеренияВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	
	Если СтруктураДействий.Свойство("ЗаполнитьЕдиницуИзмерения") Тогда
		Номенклатура = ТекущаяСтрока.Номенклатура;
		ТекущаяСтрока.ЕдиницаИзмерения = ОбщегоНазначения.ПолучитьСвойствоОбъекта(Номенклатура, "ЕдиницаИзмерения");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтавкуНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСтавкуНДС", СтруктураПараметровДействия) Тогда
		
		Номенклатура = ТекущаяСтрока.Номенклатура;
		Если ЗначениеЗаполнено(Номенклатура.СтавкаНДС) Тогда
			ТекущаяСтрока.СтавкаНДС = Номенклатура.СтавкаНДС;
		Иначе			
		
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	СтавкиНДС.Ссылка КАК СтавкаНДС
				|ИЗ
				|	Справочник.СтавкиНДС КАК СтавкиНДС
				|ГДЕ
				|	СтавкиНДС.Основная = ИСТИНА";
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				ТекущаяСтрока.СтавкаНДС = Выборка.СтавкаНДС;
			КонецЕсли;

		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСреднююЦенуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий) 
	
	Перем СтруктураПараметровДействия;

	Если СтруктураДействий.Свойство("ЗаполнитьСреднююЦену", СтруктураПараметровДействия) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|ВЫБОР КОГДА ТоварыНаСкладахОстатки.КоличествоОстаток = 0 Тогда
			|	0
			|ИНАЧЕ
			|	ТоварыНаСкладахОстатки.СуммаОстаток / ТоварыНаСкладахОстатки.КоличествоОстаток
			|КОНЕЦ КАК СредняяЦена
			|ИЗ
			|	РегистрНакопления.ТоварыНаСкладах.Остатки(
			|			&ДатаПроверки,
			|			Номенклатура = &Номенклатура
			|				И Склад = &Склад) КАК ТоварыНаСкладахОстатки";
		
		Запрос.УстановитьПараметр("ДатаПроверки", СтруктураПараметровДействия.Дата);
		Запрос.УстановитьПараметр("Номенклатура", ТекущаяСтрока.Номенклатура);
		Запрос.УстановитьПараметр("Склад", 		  СтруктураПараметровДействия.Склад);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ТекущаяСтрока.Цена = 0; 
		Иначе
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			ТекущаяСтрока.Цена = Выборка.СредняяЦена * Справочники.Номенклатура.РассчитатьКоличествоВОсновнойЕдиницеИзмерения(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ЕдиницаИзмерения, 1); 
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

Процедура ЗаполнитьЦенуПоДоговоруВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий) 
	
	Перем СтруктураПараметровДействия;

	Если СтруктураДействий.Свойство("ЗаполнитьЦенуПоДоговору", СтруктураПараметровДействия) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДоговорКонтрагентаТовары.Цена КАК Цена
			|ИЗ
			|	Справочник.ДоговорКонтрагента.Товары КАК ДоговорКонтрагентаТовары
			|ГДЕ
			|	ДоговорКонтрагентаТовары.Ссылка = &Договор
			|	И ДоговорКонтрагентаТовары.Номенклатура = &Номенклатура
			|	И ДоговорКонтрагентаТовары.ЕдиницаИзмерения = &ЕдиницаИзмерения";
		
		Запрос.УстановитьПараметр("Договор", 	  		СтруктураПараметровДействия.Договор);
		Запрос.УстановитьПараметр("Номенклатура", 		ТекущаяСтрока.Номенклатура);
		Запрос.УстановитьПараметр("ЕдиницаИзмерения", 	ТекущаяСтрока.ЕдиницаИзмерения);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ТекущаяСтрока.Цена = 0; 
		Иначе
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			ТекущаяСтрока.Цена = Выборка.Цена; 
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

Процедура ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму") Тогда
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.Количество;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры 

Процедура ПересчитатьЦенуСНДС(ТекущаяСтрока, СтруктураДействий)
	
	Перем СтруктураПараметровДействия;
	Перем СтавкаНДС;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуСНДС", СтруктураПараметровДействия) Тогда
		
		Если СтруктураПараметровДействия = Неопределено
		 Или СтруктураПараметровДействия.Свойство("СтавкаНДС", СтавкаНДС) = Ложь Тогда
			СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		КонецЕсли;
		
		ПроцентНДС = ЗначениеСтавкиНДС(СтавкаНДС);
		ТекущаяСтрока.Цена = ТекущаяСтрока.Цена * (ПроцентНДС + 100)/100;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗначениеСтавкиНДС(СтавкаНДС) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат СтавкаНДС.Ставка;
	
КонецФункции

Процедура ПересчитатьЦенуПоСуммеВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуПоСумме") Тогда
		Если ТекущаяСтрока.Количество = 0 Тогда
			ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма;
		Иначе
			ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Сумма / ТекущаяСтрока.Количество, 2);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры    

Процедура ПересчитатьСуммуСУчетомПогрешностиОкругленияВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	
	Погрешность = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСУчетомПогрешностиОкругления", Погрешность) Тогда
		
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Сумма + Погрешность;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий) 
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДС", СтруктураПараметровДействия) Тогда
		
		СтавкаНДС = Неопределено;
		Если НЕ (СтруктураПараметровДействия <> Неопределено И СтруктураПараметровДействия.Свойство("СтавкаНДС", СтавкаНДС)) Тогда
			СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		КонецЕсли;
		
		Если СтруктураПараметровДействия.УчитыватьНДС = Ложь Тогда
			ТекущаяСтрока.СуммаНДС = 0;	
		Иначе	
			ТекПроцентНДС = ЗначениеСтавкиНДС(СтавкаНДС);
			
			ТекущаяСтрока.СуммаНДС = РассчитатьСуммуНДС(ТекущаяСтрока.Сумма,
														ТекПроцентНДС,
														СтруктураПараметровДействия.ЦенаВключаетНДС);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

Функция РассчитатьСуммуНДС(Сумма, СтавкаНДС, СуммаВключаетНДС = Истина) Экспорт
	
	Если ТипЗнч(СтавкаНДС) = Тип("СправочникСсылка.СтавкиНДС") Тогда
		ЗначениеСтавкиНДС = ЗначениеСтавкиНДС(СтавкаНДС);
	Иначе
		ЗначениеСтавкиНДС = СтавкаНДС; 
	КонецЕсли;
	
	Если СуммаВключаетНДС Тогда
		СуммаНДС = Окр(Сумма - 100 * Сумма / (100 + ЗначениеСтавкиНДС), 2, РежимОкругления.Окр15как20);
	Иначе
		СуммаНДС = Окр(Сумма * ЗначениеСтавкиНДС / 100, 2, РежимОкругления.Окр15как20);
	КонецЕсли;
	
	Возврат СуммаНДС;

КонецФункции

Процедура ПересчитатьСуммуСНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСНДС", СтруктураПараметровДействия) Тогда
		
		ТекущаяСтрока.СуммаСНДС = ТекущаяСтрока.Сумма + 
				?(СтруктураПараметровДействия.ЦенаВключаетНДС ИЛИ НЕ СтруктураПараметровДействия.УчитыватьНДС, 0, ТекущаяСтрока.СуммаНДС);
		
	КонецЕсли;
	
КонецПроцедуры







