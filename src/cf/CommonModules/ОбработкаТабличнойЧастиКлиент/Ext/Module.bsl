
Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий) Экспорт 

	ТекущаяСтрокаСтруктура = ПолучитьТекущуюСтрокуСтруктурой(ТекущаяСтрока,СтруктураДействий);
	ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(ТекущаяСтрокаСтруктура, СтруктураДействий);
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока,ТекущаяСтрокаСтруктура);
	
КонецПроцедуры

Функция ПолучитьТекущуюСтрокуСтруктурой(ТекущаяСтрока, СтруктураДействий)
	Перем СтруктураПараметровДействия;
	
	СтруктураПолейТЧ = Новый Структура;
	
	Если СтруктураДействий.Свойство("ЗаполнитьЕдиницуИзмерения") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСтавкуНДС") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("СтавкаНДС");
	КонецЕсли;  
	
	Если СтруктураДействий.Свойство("ЗаполнитьСреднююЦену") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");   
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
		СтруктураПолейТЧ.Вставить("Цена");
	КонецЕсли;  
	
	Если СтруктураДействий.Свойство("ЗаполнитьЦенуПоДоговору") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");   
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
		СтруктураПолейТЧ.Вставить("Цена");
	КонецЕсли; 
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДС") Тогда
		СтруктураПолейТЧ.Вставить("Сумма"    , 0);
		СтруктураПолейТЧ.Вставить("СтавкаНДС", 0);
		СтруктураПолейТЧ.Вставить("СуммаНДС" , 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСНДС") Тогда
		СтруктураПолейТЧ.Вставить("Сумма"    , 0);
		СтруктураПолейТЧ.Вставить("СуммаНДС" , 0);
		СтруктураПолейТЧ.Вставить("СуммаСНДС", 0);
	КонецЕсли;

	Если СтруктураДействий.Свойство("ПересчитатьСуммуБезНДС") Тогда
		СтруктураПолейТЧ.Вставить("Сумма"    , 	0);
		СтруктураПолейТЧ.Вставить("СуммаНДС" , 	0);
		СтруктураПолейТЧ.Вставить("СуммаБезНДС",0);
	КонецЕсли;

	Если СтруктураДействий.Свойство("ПересчитатьСумму") ИЛИ СтруктураДействий.Свойство("ПересчитатьЦенуПоСумме") Тогда
		СтруктураПолейТЧ.Вставить("Сумма"     , 0);
		СтруктураПолейТЧ.Вставить("Цена"      , 0);
		СтруктураПолейТЧ.Вставить("Количество", 0);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтруктураПолейТЧ, ТекущаяСтрока);
	
	Возврат СтруктураПолейТЧ;
	
КонецФункции
