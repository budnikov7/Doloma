
Функция ПолучитьОстатки(ИмяДокумента, ИмяТЧ, ИмяНоменклатуры="Номенклатура", Документ, Склад) Экспорт

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |	Товары.Номенклатура КАК Номенклатура,
        |	СУММА(Товары.Количество) КАК Количество,
        |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения
        |ПОМЕСТИТЬ Товары
        |ИЗ
        |	Документ.СписаниеТоваров.Товары КАК Товары
        |ГДЕ
        |	Товары.Ссылка = &Документ
        |	И Товары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
        |
        |СГРУППИРОВАТЬ ПО
        |	Товары.Номенклатура,
        |	Товары.ЕдиницаИзмерения
        |
        |ИНДЕКСИРОВАТЬ ПО
        |	Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	Товары.Номенклатура КАК Номенклатура,
        |	Товары.Количество КАК Количество,
        |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
        |	ЕСТЬNULL(НоменклатураЕдиницыИзмерения.Коэффициент, 1) КАК Кофффициент
        |ПОМЕСТИТЬ втТоварыСКоэффициентами
        |ИЗ
        |	Товары КАК Товары
        |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ЕдиницыИзмерения КАК НоменклатураЕдиницыИзмерения
        |		ПО Товары.ЕдиницаИзмерения = НоменклатураЕдиницыИзмерения.ЕдиницаИзмерения
        |			И Товары.Номенклатура = НоменклатураЕдиницыИзмерения.Ссылка
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТоварыСКоэффициентами.Номенклатура КАК Номенклатура,
        |	ПРЕДСТАВЛЕНИЕССЫЛКИ(втТоварыСКоэффициентами.Номенклатура) КАК НоменклатураПредставление,
        |	втТоварыСКоэффициентами.Количество КАК Количество,
        |	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
        |	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
        |	ПРЕДСТАВЛЕНИЕССЫЛКИ(втТоварыСКоэффициентами.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
        |	ЕСТЬNULL(Остатки.СуммаОстаток, 0) / ВЫБОР
        |		КОГДА ЕСТЬNULL(Остатки.КоличествоОстаток, 0) = 0
        |			ТОГДА 1
        |		ИНАЧЕ ЕСТЬNULL(Остатки.КоличествоОстаток, 0)
        |	КОНЕЦ КАК Цена,
        |	&Склад КАК Склад,
        |	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) / втТоварыСКоэффициентами.Кофффициент КАК КоличествоОстатокВЕдиницахДокумента,
        |	втТоварыСКоэффициентами.Кофффициент КАК Кофффициент
        |ИЗ
        |	втТоварыСКоэффициентами КАК втТоварыСКоэффициентами
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
        |				&МоментВремени,
        |				Склад = &Склад
        |					И Номенклатура В
        |						(ВЫБРАТЬ
        |							Товары.Номенклатура КАК Номенклатура
        |						ИЗ
        |							Товары КАК Товары)) КАК Остатки
        |		ПО втТоварыСКоэффициентами.Номенклатура = Остатки.Номенклатура";
    
    Запрос.Текст = СтрЗаменить(Запрос.Текст,"Товары.Номенклатура КАК",СтрШаблон("Товары.%1 КАК",ИмяНоменклатуры));  
    Запрос.Текст = СтрЗаменить(Запрос.Текст,"СписаниеТоваров.Товары",ИмяДокумента+"."+ИмяТЧ);  
    Запрос.УстановитьПараметр("Документ", Документ);
    Запрос.УстановитьПараметр("МоментВремени", Документ.МоментВремени()); 
	Запрос.УстановитьПараметр("Склад", Склад);
    Выборка = Запрос.Выполнить().Выбрать();
    
    Возврат Выборка;
    
КонецФункции // ПолучитьОстатки()  






