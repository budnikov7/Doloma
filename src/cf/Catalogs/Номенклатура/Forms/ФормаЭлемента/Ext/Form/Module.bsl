 
//Обработчики событий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПереданныеДополнительныеПараметры = Неопределено;
		Если Параметры.Свойство("ДополнительныеПараметры", ПереданныеДополнительныеПараметры) 
				И ПереданныеДополнительныеПараметры <> Неопределено Тогда
			ВидНоменклатуры = Неопределено;
			Если ТипЗнч(ПереданныеДополнительныеПараметры) = Тип("Структура") 
					И ПереданныеДополнительныеПараметры.Свойство("ВидНоменклатуры",ВидНоменклатуры) Тогда
			     Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
					Объект.ВидНоменклатуры 	= ВидНоменклатуры;
					Объект.СтавкаНДС 		= ВидНоменклатуры.СтавкаНДС;
					Объект.ТипНоменклатуры 	= ВидНоменклатуры.ТипНоменклатуры;
					Если ЗначениеЗаполнено(ВидНоменклатуры.ЕдиницаИзмерения) Тогда
						НоваяЕдиница 					= Объект.ЕдиницыИзмерения.Добавить();
						НоваяЕдиница.ЕдиницаИзмерения 	= ВидНоменклатуры.ЕдиницаИзмерения; 
						НоваяЕдиница.Коэффициент 		= 1; 
						НоваяЕдиница.Основная 			= Истина;
						Элементы.ЕдиницыИзмеренияОсновнаяЕдиницаИзмерения.Пометка = Истина;
					    Объект.ЕдиницаИзмерения 		= ВидНоменклатуры.ЕдиницаИзмерения;
					КонецЕсли;
				 КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(Объект.ЕдиницаИзмерения) Тогда
		Отказ = Истина;
		Сообщить("Не указана основная единица измерения");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ОбновитьСписокПартийСемениВБыках");
	
КонецПроцедуры

&НаКлиенте
Процедура БыкПриИзменении(Элемент)
	
	СформироватьНаименованиеСемени();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСемениПриИзменении(Элемент)
	
	СформироватьНаименованиеСемени();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницыИзмеренияПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ЕдиницыИзмерения.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		
		Элементы.ЕдиницыИзмеренияОсновнаяЕдиницаИзмерения.Пометка = ТекущиеДанные.Основная;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

//Общие процедуры

&НаКлиенте
Процедура ОсновнаяЕдиницаИзмерения(Команда)
	
	ТекущиеДанные = Элементы.ЕдиницыИзмерения.ТекущиеДанные;
	
	Если Не ТекущиеДанные = Неопределено Тогда
		
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ЕдиницаИзмерения) Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПустоеОповещение", ОбщегоНазначения);
			ПоказатьПредупреждение(ОписаниеОповещения, "В строке не выбрана единица измерения. Сделать основной невозможно");
			Возврат;
			
		КонецЕсли;
		
		Если ТекущиеДанные.Основная Тогда
			
			Объект.ЕдиницаИзмерения = "";
			ТекущиеДанные.Основная = Ложь;
			Элементы.ЕдиницыИзмеренияОсновнаяЕдиницаИзмерения.Пометка = ТекущиеДанные.Основная;
			
		Иначе
			
			СтараяЕдиница = Объект.ЕдиницыИзмерения.НайтиСтроки(Новый Структура("Основная", Истина));
			
			Для  каждого СтрокаСтаройЕдиницы Из СтараяЕдиница Цикл
				
				СтрокаСтаройЕдиницы.Основная = Ложь;
				
			КонецЦикла;
			
			Объект.ЕдиницаИзмерения = ТекущиеДанные.ЕдиницаИзмерения;
			ТекущиеДанные.Основная = Истина;
			Элементы.ЕдиницыИзмеренияОсновнаяЕдиницаИзмерения.Пометка = ТекущиеДанные.Основная;
			
		КонецЕсли;
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьНаименованиеСемени()
	
	Объект.Наименование = "" + СокрЛП(Объект.Бык) + " ("+ СокрЛП(Объект.ТипСемени) +")";
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	ЭтоСемя = (Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Семя"));
	ЭтоКорм = (Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Корм"));
	Элементы.ТипСемени.Видимость 			= ЭтоСемя;
	Элементы.Бык.Видимость 					= ЭтоСемя;
	Элементы.ВидКорма.Видимость				= ЭтоКорм;
	Элементы.ГруппаКорма.Видимость			= ЭтоКорм;
	Элементы.ФормаГруппаПерейти.Видимость 	= ЭтоКорм;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницыИзмеренияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
    Если НоваяСтрока И Не Копирование Тогда
        Элемент.ТекущиеДанные.Коэффициент = 1;
		Если НЕ ЗначениеЗаполнено(Объект.ЕдиницаИзмерения) Тогда
			Элемент.ТекущиеДанные.Основная = Истина;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницыИзмеренияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока И НЕ ОтменаРедактирования Тогда
		Если Элемент.ТекущиеДанные.Основная = Истина И ЗначениеЗаполнено(Элемент.ТекущиеДанные.ЕдиницаИзмерения) Тогда
			Объект.ЕдиницаИзмерения = Элемент.ТекущиеДанные.ЕдиницаИзмерения;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПитательности(Команда)  
	
	СтрОтбора = Новый Структура("Номенклатура", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", СтрОтбора);
	ОткрытьФорму("РегистрСведений.ТаблицаПитательностиКормов.ФормаСписка", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры













