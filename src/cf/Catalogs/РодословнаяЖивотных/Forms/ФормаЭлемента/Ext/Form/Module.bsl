#Область ОбработчикиСобытийФормы  

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СформироватьДеревоСвязей();
		Животное = НайтиЖивотное(Объект.Ссылка);
		ЖивотноеСтарое = Животное;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Животное <> ЖивотноеСтарое Тогда
		Если ЗначениеЗаполнено(ЖивотноеСтарое) Тогда
			ЖивотноеСтароеОбъект = ЖивотноеСтарое.ПолучитьОбъект();
			ЖивотноеСтароеОбъект.Родословная = Справочники.РодословнаяЖивотных.ПустаяСсылка();
		    ЖивотноеСтароеОбъект.Записать();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Животное) Тогда
			ЖивотноеОбъект = Животное.ПолучитьОбъект();
			ЖивотноеОбъект.Родословная = Объект.Ссылка;
		    ЖивотноеОбъект.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЖивотноеПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРодственныхСвязейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ДеревоРодственныхСвязей.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Предок) Тогда
		ПоказатьЗначение(,ТекущиеДанные.Предок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КличкаПриИзменении(Элемент)
	ЗаполнитьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура РегистрационныйНомерПриИзменении(Элемент)
	ЗаполнитьНаименование();
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СоздатьБыка(Команда)
	
	СтруктураЗаполнения = Новый Структура(); 
	
	СтруктураЗаполнения.Вставить("Наименование",	Объект.Наименование); 
	СтруктураЗаполнения.Вставить("Кличка", 			Объект.Кличка); 
	СтруктураЗаполнения.Вставить("КодСемени", 		Объект.КодСемени); 
	СтруктураЗаполнения.Вставить("ДатаРождения", 	Объект.ДатаРождения);
	СтруктураЗаполнения.Вставить("Порода", 			Объект.Порода);
	СтруктураЗаполнения.Вставить("ПолнаяКличка", 	Объект.КличкаНаАнглийском);
	СтруктураЗаполнения.Вставить("Родословная", 	Объект.Ссылка);
	СтруктураЗаполнения.Вставить("БетаКазеин", 		Объект.БетаКазеин);
	СтруктураЗаполнения.Вставить("КаппаКазеин", 	Объект.КаппаКазеин);
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", СтруктураЗаполнения);
	ОткрытьФорму("Справочник.БыкиПлеменные.ФормаОбъекта", ПараметрыФормы); 

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФукции

&НаСервереБезКонтекста
Функция НайтиЖивотное(Родословная)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Животные.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Животные КАК Животные
	|ГДЕ
	|	Животные.Родословная = &Родословная
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БыкиПлеменные.Ссылка
	|ИЗ
	|	Справочник.БыкиПлеменные КАК БыкиПлеменные
	|ГДЕ
	|	БыкиПлеменные.Родословная = &Родословная";
	Запрос.УстановитьПараметр("Родословная",Родословная);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка; 
	КонецЕсли;		
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьНаименование() 
	
	Объект.Наименование = ОбщегоНазначенияКлиент.ПолучитьНаименованиеРодословной(Объект);
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.СоздатьБыка.Видимость = НЕ ЗначениеЗаполнено(Животное);
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоСвязей()

	ДеревоСвязей = РегистрыСведений.РодственныеСвязи.СформироватьДеревоСвязей(Объект.Ссылка);
	ЗначениеВДанныеФормы(ДеревоСвязей, ДеревоРодственныхСвязей);

КонецПроцедуры
	
#КонецОбласти










