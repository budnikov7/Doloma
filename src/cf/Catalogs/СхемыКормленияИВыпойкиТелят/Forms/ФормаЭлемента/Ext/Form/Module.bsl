   
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьСхемуПоМакету();
		ЗаполнитьОписаниеСхемыПоМакету();
	КонецЕсли; 
	
КонецПроцедуры  

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОписаниеСхемы = ТекущийОбъект.ОписаниеСхемы.Получить();
КонецПроцедуры   

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ОписаниеСхемы = Новый ХранилищеЗначения(ОписаниеСхемы);
КонецПроцедуры
   
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьДниСодержания();
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбновитьДниСодержания();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДниСодержания()

	Для каждого Строчка Из Объект.Схема Цикл
		Строчка.ДниСодержания = СтрШаблон("%1-%2",Строчка.НомерСтроки * 7 - 6, Строчка.НомерСтроки * 7);	
	КонецЦикла;	 

КонецПроцедуры // ОбновитьДниСодержания()

&НаКлиенте
Процедура СхемаПриИзменении(Элемент)
	ОбновитьДниСодержания();
КонецПроцедуры

&НаКлиенте
Процедура СхемаМолокоВДеньПриИзменении(Элемент) 
	
	ТекущиеДанные = Элементы.Схема.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.Молоко = ТекущиеДанные.МолокоВДень * 7;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СхемаСтартернаяСмесьВДеньПриИзменении(Элемент)   
	
	ТекущиеДанные = Элементы.Схема.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.СтартернаяСмесь = ТекущиеДанные.СтартернаяСмесьВДень * 7;	
	КонецЕсли; 
	
КонецПроцедуры  

&НаКлиенте
Процедура СхемаПСРВДеньПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Схема.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ПСР = ТекущиеДанные.ПСРВДень * 7;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)    
	
	ТабДок = Новый ТабличныйДокумент;
    ЗаполнитьТабличныйДокумент(ТабДок);
	
	ТабДок.АвтоМасштаб 			= Истина;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.ОтображатьСетку 		= Ложь;
	ТабДок.Защита 				= Ложь;
	ТабДок.ТолькоПросмотр 		= Ложь;
	ТабДок.ОтображатьЗаголовки 	= Ложь;
	
	ТабДок.Показать("Печать");

КонецПроцедуры    

&НаСервере
Процедура ЗаполнитьТабличныйДокумент(ТабДок)

	Макет = ПолучитьМакетПечати("МакетПечати"); 
	
	оШапкаОписания = ОписаниеСхемы.ПолучитьОбласть("Шапка"); 
	оШапка  = Макет.ПолучитьОбласть("Шапка");
	оШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	оСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
	оИтого = Макет.ПолучитьОбласть("Итого");
	
	оШапка.Параметры.ГлавныйЗоотехник = ПолучитьГлавногоЗоотехника();
	ТабДок.Вывести(оШапка);
	ТабДок.Вывести(оШапкаТаблицы);
	
	Для Каждого Строчка Из Объект.Схема Цикл
		оСтрока.Параметры.Заполнить(Строчка);
		Если Строчка.ПСРВДень = 0.05 Тогда
			оСтрока.Параметры.ПСРВДень = "0,05 (приучение)";
		КонецЕсли;
		табДок.Вывести(оСтрока);
	КонецЦикла; 
	
	оИтого.Параметры.Молоко = Объект.Схема.Итог("Молоко");
	оИтого.Параметры.СтартернаяСмесь = Объект.Схема.Итог("СтартернаяСмесь");
	оИтого.Параметры.ПСР = Объект.Схема.Итог("ПСР"); 
	ТабДок.Вывести(оИтого);
	
	ВремТабДок = Новый ТабличныйДокумент;
	ОбластьЯчеек = ВремТабДок.Вывести(оШапкаОписания);
	ОбластьЯчеек.СоздатьФорматСтрок();
	
	ТабДок.Вывести(ВремТабДок);
	
КонецПроцедуры // ()


&НаСервереБезКонтекста
Функция ПолучитьМакетПечати(НазваниеМакета)

	Возврат Справочники.СхемыКормленияИВыпойкиТелят.ПолучитьМакет(НазваниеМакета);	

КонецФункции // ПолучитьМакетПечати()  

&НаСервереБезКонтекста
Функция ПолучитьГлавногоЗоотехника()
	
	ПользовательГлЗоотехник = "";
	РольГлЗоотехник = Метаданные.Роли.ГлавныйЗоотехник;
    Для Каждого ПользовательИзМассива Из ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
        Если ПользовательИзМассива.Роли.Содержит(РольГлЗоотехник) Тогда
            ПользовательГлЗоотехник = ПользовательИзМассива.Имя;
            Прервать; 
        КонецЕсли;
	КонецЦикла;
	Возврат ПользовательГлЗоотехник;

КонецФункции // ПолучитьГлавногоЗоотехника()

&НаСервере
Процедура ЗаполнитьСхемуПоМакету()  
	
	МакетСхемы = ПолучитьМакетПечати("МакетСхемыПоУмолчанию");
	//МакетСхемы = Справочники.СхемыКормленияИВыпойкиТелят.ПолучитьМакет("МакетСхемыПоУмолчанию);
	
	КоличествоСтрок = МакетСхемы.ВысотаТаблицы;
	КоличествоКолонок = МакетСхемы.ШиринаТаблицы;
	
    Для СчетчикСтрок = 2 По КоличествоСтрок Цикл
	
		НоваяСтрокаСхемы = Объект.Схема.Добавить();	 
		
		Для СчетчикКолонок = 1 По КоличествоКолонок Цикл
		
			НоваяСтрокаСхемы[МакетСхемы.Область(1,СчетчикКолонок,1,СчетчикКолонок).Текст] = 
				МакетСхемы.Область(СчетчикСтрок,СчетчикКолонок,СчетчикСтрок,СчетчикКолонок).Текст;		
		
		КонецЦикла;
		
		НоваяСтрокаСхемы.Молоко = НоваяСтрокаСхемы.МолокоВДень * 7;
		НоваяСтрокаСхемы.СтартернаяСмесь = НоваяСтрокаСхемы.СтартернаяСмесьВДень * 7;
		НоваяСтрокаСхемы.ПСР = НоваяСтрокаСхемы.ПСРВДень * 7;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСхемуПоМакету()  

&НаСервере
Процедура ЗаполнитьОписаниеСхемыПоМакету()

	МакетСхемы = ПолучитьМакетПечати("МакетОписанияСхемы");
	ОписаниеСхемы = МакетСхемы;

КонецПроцедуры // ЗаполнитьОписаниеСхемыПоМакету()















