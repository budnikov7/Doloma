
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МассаРаздачиПриИзменении(Элемент)
	РассчитатьКоэффициентКормленияИСВ();
КонецПроцедуры

&НаКлиенте
Процедура КоэффициентКормленияПриИзменении(Элемент)
	РассчитатьМассуРаздачиИСВ();
КонецПроцедуры

&НаКлиенте
Процедура КоличествоГоловПриИзменении(Элемент)
	РассчитатьМассуРаздачиИСВ();
КонецПроцедуры

&НаКлиенте
Процедура РационПриИзменении(Элемент)
	РассчитатьМассуРаздачиИСВ();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РассчитатьМассуРаздачиИСВ()
	
	Если ЗначениеЗаполнено(Объект.Рацион) Тогда
		ВесРациона = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Рацион, "Вес");
		Если Объект.КоэффициентКормления = 0 Тогда
			Объект.КоэффициентКормления = 1;
		КонецЕсли;
		Объект.МассаРаздачи = Объект.КоэффициентКормления * Объект.КоличествоГолов * ВесРациона;
	Иначе
		ОбщегоНазначения.СообщитьПользователю("Для расчета массы раздачи заполните рацион.",,"Рацион","Объект");
	КонецЕсли;
	
	РассчитатьСВ();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСВ()
	
	Если НЕ Объект.Рацион.Пустая() И Объект.КоличествоГолов > 0 Тогда
		ПроцентСВ = Справочники.Рационы.ПроцентСВРациона(Объект.Рацион);
		Объект.СВ = Объект.МассаРаздачи * ПроцентСВ / Объект.КоличествоГолов;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьКоэффициентКормленияИСВ()
	
	ОшибкиЗаполнения = "";
	
	Если Объект.КоличествоГолов > 0 Тогда
		
		Если ЗначениеЗаполнено(Объект.Рацион) Тогда
			ВесРациона = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Рацион, "Вес");
			Если ВесРациона = 0 Тогда
				ОшибкиЗаполнения = "Расчет коэффициента кормления не выполнен, т.к. вес рациона равен нулю.";
			КонецЕсли;
		Иначе
			ОшибкиЗаполнения = "Для расчета коэффициента кормления заполните рацион.";
		КонецЕсли;
		
		Если ОшибкиЗаполнения <> "" Тогда
			ОбщегоНазначения.СообщитьПользователю("Для расчета коэффициента кормления заполните рацион.",,"Рацион","Объект");
			Возврат;
		КонецЕсли;
		
		Если ВесРациона <> 0 Тогда
			Объект.КоэффициентКормления = Объект.МассаРаздачи / Объект.КоличествоГолов / ВесРациона;
		КонецЕсли;
		
	КонецЕсли;
	
	РассчитатьСВ();
	
КонецПроцедуры

#КонецОбласти